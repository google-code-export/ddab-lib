#summary Resource File Unit Example 5.
<wiki:comment>
$Rev$
$Date$
</wiki:comment>

= Example #5: Adding to or modifying a resource's data =

While the code in the [ResFileUnit Resource File Unit] does not understand the various resource data formats it does assist in reading, adding, updating and deleting the raw resource data. The _[TPJResourceEntry TPJResourceEntry]_ class's _Data_ property exposes the resource data as a _TStream_ which means that we can use normal stream handling techniques to access the data.

== Reading data ==

The following code fragment shows how to read all the data from a resource entry to a buffer.

{{{
var
  Entry: TPJResourceEntry;
  Buf: PByte;
begin
  ...
  // Make sure Entry references a resource object
  ...
  // Create buffer of required size
  GetMem(Buf, Entry.DataSize);
  try
    // Make sure resource data stream at start
    Entry.Data.Position := 0;
    // Read all resource data into buffer
    Entry.Data.ReadBuffer(Buf^, Entry.DataSize);
    // Rewind data stream again
    Entry.Data.Position := 0;
    ...
    // Do something with Buf
    ...
  finally
    // Release buffer
    FreeMem(Buf);
  end;
end;
}}}

We first set the buffer to the required size by getting the size of the resource from the _[TPJResourceEntry TPJResourceEntry]_'s _!DataSize_ property (you could also use the _Data.Size_ property). We now ensure the resource data stream is positioned at the start (you can't assume this!) then read all the data into the buffer using _TStream_'s _!ReadBuffer_ method. We then reposition the stream ready for the next use. Having processed the data in the buffer in some way we finally free it.

It may be more convenient to copy the resource data to another stream before processing it. The next example illustrates this by storing the resource data in a file named `ResEntry.dat`:

{{{
var
  Entry: TPJResourceEntry;
  FS: TFileStream;
begin
  ...
  // Make sure Entry references a resource object
  ...
  // Open stream onto new file
  FS := TFileStream.Create('ResEntry.dat', fmCreate);
  try
    // Copy resource data to file
    FS.CopyFrom(Entry.Data, 0);
    Entry.Data.Position := 0;
  finally
    // Close the file
    FS.Free;
  end;
end;
}}}

Here we first open a stream onto a new file. We then use _TStream_'s _!CopyFrom_ method to copy the whole of the resource data to the file stream. By specifying a size of `0` in the _!CopyFrom_ method, _TStream_ automatically positions the resource data stream at the start and copies the whole stream, so we don't need to position it first. Once again we reset the resource data stream once we are done.

== Deleting data ==

It is very easy to delete all the data in a resource: simply set the data stream's size to `0`:

{{{
var
  Entry: TPJResourceEntry;
begin
  ...
  // Make sure Entry references a resource object
  ...
  Entry.Data.Size := 0;
  ...
end;
}}}

You must set the _Size_ property of the entry's _Data_ property here: you can't set the _!DataSize_ property since it is read only.

== Writing data ==

We can add data to an existing resource quite simply. Let us first look at how to overwrite any existing data and then show how to append data to an existing resource. For the purposes of this example, assume we have a user defined resource that stores some plain text. _Entry_ is a _[TPJResourceEntry TPJResourceEntry]_ object that references our resource. We will replace any existing data with the text `'Hello World'`.

{{{
var
  Entry: TPJResourceEntry;
  Text: string;
begin
  ...
  // Make sure Entry references a resource object
  ...
  // Delete any existing data
  Entry.Data.Size := 0;
  // Write the required text
  Text := 'Hello World';
  Entry.Data.WriteBuffer(Pointer(Text)^, Length(Text) * SizeOf(Char));
  // Position data ready for reading
  Entry.Data.Position := 0;
  ...
end;
}}}

The only point of note here is the use of one of the normal idioms is writing a string to a stream: we can't just do `WriteBuffer(Text, Length(Text))`, but must specify the start of the string (`Pointer(Text)^`) and pass the correct length: remember that since Delphi 2009 `SizeOf(Char)` is now `2`!

Now let's look at how we add more text to the end of the resource data:

{{{
var
  Entry: TPJResourceEntry;
  Text: string;
begin
  ...
  // Make sure Entry references a resource object
  ...
  // Move to end of existing data
  Entry.Data.Seek(0, soFromEnd);
  // Write new text
  Text := ' From DelphiDabbler';
  Entry.Data.WriteBuffer(Pointer(Text)^, Length(Text) * SizeOf(Char));
  Text := #13#10'www.delphidabbler.com';
  Entry.Data.WriteBuffer(Pointer(Text)^, Length(Text) * SizeOf(Char));
  // Reposition stream to start
  Entry.Data.Position := 0;
  ...
end;
}}}

Here we move the stream pointer to the end of the stream so the text we write is appended to any existing data. Running the two examples in sequence leaves the resource containing the following two lines of text:

  `Hello World from DelphiDabbler`<br />
  `www.delphidabbler.com`

*Links:*

  * [ResFileExample6 Next Example]
  * [ResFileExample4 Previous Example]
  * Back to [ResFileExamples List of Examples]
